" Preamble --------------------------------------------------------------- {{{

set nocompatible

set runtimepath+=~/.vim/bundle/neobundle.vim/

filetype off
call neobundle#begin(expand('~/.vim/bundle/'))

NeoBundleFetch 'Shougo/neobundle.vim'

" Eye candy
NeoBundle 'altercation/vim-colors-solarized'
NeoBundle 'bling/vim-airline'

" Language support
NeoBundle 'klen/python-mode'

" Snippets & completion
if has('python')
	" Ultisnips complains about python if it's missing
	NeoBundle 'SirVer/ultisnips'
endif
NeoBundle 'honza/vim-snippets'

" Utilties
NeoBundle 'godlygeek/tabular'
NeoBundle 'scrooloose/syntastic'
NeoBundle 'scrooloose/nerdtree'
NeoBundle 'Raimondi/delimitMate'
NeoBundle 'tpope/vim-fugitive' " git integration


" Conditional Plugin disables {{{
if v:version < '703' || !has('python')
	" Python-mode is kinda busted on RHEL6
	NeoBundleDisable 'python-mode'
endif
" }}}


call neobundle#end()
filetype plugin indent on

NeoBundleCheck

" }}}

" General Settings ------------------------------------------------------- {{{

set nobackup            " don't create backup files
set ruler               " display the cursor position
set nohlsearch          " don't highlight search terms
set bs=2                " allow useful backspace in insert mode
set nomodeline          " Disable modelines for security

" Leader {{{
"
let mapleader = ","
let maplocalleader = "\\"

" }}}

" Tabs and indenting {{{

set tabstop=4
set shiftwidth=4
set shiftround

" Enable fun indenting/functions
set nosi
set autoindent

" }}}

" Vim Tabs {{{

nnoremap tl :tabnext<CR>
nnoremap th :tabprev<CR>
nnoremap tf :tabfirst<CR>
nnoremap tg :tablast<CR>
nnoremap tn :tabnew<CR>
nnoremap td :tabclose<CR>

" }}}

" Color/style {{{

set bg=dark             " black terminal background
syntax on               " syntax highlighting is what the cool kids do
set synmaxcol=800       " Don't try to highlight lines longer than 800 chars.

" Line numbers are nice
set nu

" Solarize that shit
let g_solarized_visibility='low'
let g:solarized_termcolors=256
colorscheme solarized

" Pre-solarized settings {{{
" Make listchars more muted in color
"hi SpecialKey guifg=#5f87df ctermfg=68

" Fix crappy Pmenu colors
"hi Pmenu guifg=#ffffff guibg=#444444 ctermfg=255 ctermbg=238
"hi PmenuSel guifg=#000000 guibg=#b1d631 ctermfg=0 ctermbg=148
" }}}

" }}}

" Folding {{{

" map space-bar to toggle folding
noremap <space> za

function! MyFoldText() " {{{
	let line = getline(v:foldstart)

	let nucolwidth = &fdc + &number * &numberwidth
	let windowwidth = winwidth(0) - nucolwidth - 3
	let foldedlinecount = v:foldend - v:foldstart

	" expand tabs into spaces
	let onetab = strpart('          ', 0, &tabstop)
	let line = substitute(line, '\t', onetab, 'g')

	let line = strpart(line, 0, windowwidth - 2 -len(foldedlinecount))
	let fillcharcount = windowwidth - len(line) - len(foldedlinecount)
	return line . '…' . repeat(" ",fillcharcount) . foldedlinecount . '…' . ' '
endfunction " }}}
set foldtext=MyFoldText()

" }}}

" Character Listing {{{

set list
"set listchars=tab:▸\ ,eol:¬,extends:❯,precedes:❮
set listchars=tab:\>\-


augroup trailing
	au!
	au BufReadPost * :match ErrorMsg '\s\+$'
	au InsertEnter * :highlight clear ErrorMsg
	au InsertLeave * :highlight ErrorMsg term=reverse cterm=reverse ctermfg=124 guifg=White guibg=Red
augroup END

" }}}

" Wildmenu completion {{{

set wildmenu
set wildmode=list:longest

set wildignore+=.hg,.git,.svn                    " Version control
set wildignore+=*.aux,*.out,*.toc                " LaTeX intermediate files
set wildignore+=*.jpg,*.bmp,*.gif,*.png,*.jpeg   " binary images
set wildignore+=*.o,*.obj,*.exe,*.dll,*.manifest " compiled object files
set wildignore+=*.sw?                            " Vim swap files

set wildignore+=*.pyc                            " Python byte code

set wildignore+=*.orig                           " Merge resolution files

" }}}

" Line Return {{{

	" Return to line conditional on g_file_line_return
fun! File_line_return()
	if exists('b:file_line_return') && b:file_line_return != 1
		return
	endif
	if line("'\"") > 0 && line("'\"") <= line("$") |
		execute 'normal! g`"zvzz' |
	endif
endfunction

" Make sure Vim returns to the same line when you reopen a file.
augroup line_return
	au!
	au BufReadPost * call File_line_return()
augroup END

" }}}

" Terminal settings {{{

set ttyfast
set lazyredraw

" support 256 color terminals
if &term =~ "xterm" || &term =~ "256color"
	set t_Co=256
endif

" }}}

" Key Binding Overrides {{{

" Too often I hit the shifted version of these keys while line-selecting text.
" Rebind the keys to pretend that I didn't hold the shift key down too long.
nnoremap <S-k> k
vnoremap <S-k> k
nnoremap <S-H> h
vnoremap <S-H> h
nnoremap <S-L> l
vnoremap <S-L> l

" reset foldmethod and redraw screen when folding is hosed
nnoremap <C-L> :let &fdm = &fdm<CR>:redraw!<CR>

" }}}

" QuickFix Overrides {{{

" Support jsstyle/cstyle output
set efm+=%f:\ %l:%m

" }}}

" CTags {{{

set tags=./tags;~/

" }}}


" }}}

" Plugin Settings ------------------------------------------------------- {{{

" Python-Mode {{{

let g:pymode_rope_auto_project = 1
let g:pymode_lint_mccabe_complexity = 9

" }}}

" Tabular {{{

vnoremap <leader>tb :Tabular block<cr>
nnoremap <leader>tb :Tabular block<cr>

" }}}

" Syntastic {{{

nnoremap <leader>sc :SyntasticCheck<cr>
nnoremap <leader>se :Errors<cr>
nnoremap <leader>st :SyntasticToggleMode<cr>

let g:syntastic_mode_map = {}
" Puppet checkers are too slow for active mode
let g:syntastic_mode_map['passive_filetypes'] = ['puppet']
let g:syntastic_javascript_checkers = ['jshint']

" }}}

" UltiSnips {{{

let g:UltiSnipsExpandTrigger = "<tab>"
let g:UltiSnipsJumpForwardTrigger = "<tab>"
let g:UltiSnipsJumpBackwardTrigger = "<s-tab>"

" }}}

" airline {{{

" always show
set laststatus=2
" airline disables virtual column by default (and that's stupid)
let g:airline_section_z = '%3p %3l:%3c%3V'
let g:airline_theme = 'murmur'

" }}}

" }}}

" Filetype-specific ------------------------------------------------------ {{{

" C {{{

augroup ft_c
	au!

	au FileType c setlocal foldmethod=marker foldmarker={,}
	au FileType c setlocal list!
augroup END

" }}}

" Javascript {{{

augroup ft_javascript
	au!

	" Highlight json files as javascript
	au BufRead,BufNewFile *.json set filetype=javascript

	au Filetype javascript call JavaScriptFold()
	au Filetype javascript setlocal tabstop=2
	au Filetype javascript setlocal shiftwidth=2
	au Filetype javascript setlocal expandtab
augroup END

" }}}

" Git Commit {{{

augroup ft_gitcommit
	au!
	" Don't bother with listchars in git commit messages
	au FileType gitcommit setlocal list!
	" Start off on the first line
	au FileType gitcommit let b:file_line_return=0
augroup END

" }}}

" Makefile {{{

augroup ft_make
	au!
	" Don't bother with listchars in git commit messages
	au FileType make setlocal ts=8
augroup END

" }}}

" Markdown {{{

	au BufRead,BufNewFile *.md set filetype=markdown
	au BufRead,BufNewFile *.restdown set filetype=markdown

" }}}

" Puppet {{{

augroup ft_puppet
	au!

	au Filetype puppet setlocal foldmethod=marker
	au Filetype puppet setlocal foldmarker={,}
	" Conform to style guide for spaces/tabs
	au Filetype puppet setlocal tabstop=2
	au Filetype puppet setlocal shiftwidth=2
	au Filetype puppet setlocal expandtab
	" perform correct tabbing for => lists
	au Filetype puppet AddTabularPattern! block /^[^=]*\zs=>

	au Filetype puppet let g:syntastic_puppet_lint_arguments=
\'--no-class_parameter_defaults-check ' .
\'--no-documentation-check ' .
\'--no-autoloader_layout-check '
augroup END

" }}}

" Ruby {{{

augroup ft_ruby
	au!

	au Filetype ruby setlocal foldmethod=syntax
	" Conform to style guide for spaces/tabs
	au Filetype ruby setlocal tabstop=2
	au Filetype ruby setlocal shiftwidth=2
	au Filetype ruby setlocal expandtab

" }}}

" eRuby {{{

augroup ft_eruby
	au!

	au Filetype eruby setlocal foldmethod=syntax
	" Conform to style guide for spaces/tabs
	au Filetype eruby setlocal tabstop=2
	au Filetype eruby setlocal shiftwidth=2
	au Filetype eruby setlocal expandtab

" }}}

" Vim {{{

augroup ft_vim
	au!

	au FileType vim setlocal foldmethod=marker
	au FileType help setlocal textwidth=78
	au BufWinEnter *.txt if &ft == 'help' | wincmd L | endif
augroup END

" }}}

" }}}
